archetype competition

variable organizer : role = @tz1Lc2qBKEWCBeDU8npG6zCeCqpmaegRi6Jg

(* start date *)
variable startdate : date = now

(* deadline *)
variable deadline : date = now + 10m

variable prize : tez = 1000tz

variable oracle : role = @tz1bfVgcJC4ukaQSHUe1EbrUd5SekXeP9CWk

asset submission {
  competitor : role;
  score      : nat;
  timestamp  : date;
}

(* state machine *)
states =
 | Created     initial
 | InProgress
 | Done
 | Closed

transition confirm () {
  from Created
  to InProgress when { now > startdate }
}

entry submit (packed_score : bytes, signed_score : signature) {
  require {
    c1 : state = InProgress;
    c2 : now <= deadline;
  }

  effect {
    if check_signature(oracle, signed_score, packed_score) then (
      match_option unpack<nat>(packed_score) with
      | some(s) ->
        submission.add ({ competitor = caller;
                          score = s;
                          timestamp = now })
      | none -> fail("cannot unpack score")
      end
    ) else fail("not signed by oracle");
  }
}

transition decide () {
  require {
    c3 : now > deadline;
  }
  from InProgress
  to Done
  with effect {
    var submissions = submission.sort(desc(score), timestamp);
      if submissions.count() >= 3
      then (
        var first  = submissions.nth(0);
        var second = submissions.nth(1);
        var third  = submissions.nth(2);
        var q1 = 0.5 * prize;
        var q2 = 0.3 * prize;
        var q3 = 0.2 * prize;
        transfer q1 to first;
        transfer q2 to second;
        transfer q3 to third;
        transfer (prize - q1 - q2 - q3) to organizer)
        else if (submissions.count() >= 2)
             then (
               var first  = submissions.nth(0);
               var second = submissions.nth(1);
               var q1 = 0.6 * prize;
               var q2 = 0.4 * prize;
               transfer q1 to first;
               transfer q2 to second;
               transfer (prize - q1 - q2) to organizer)
             else if (submissions.count() >= 1)
               then (
                 var first = submissions.nth(0);
                 transfer prize to first)
               else transfer prize to organizer
  }
}
